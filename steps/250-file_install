#!/usr/bin/perl -w
# Install files and symlink
# vim:tw=100 sw=2 expandtab ft=perl
#
# Example:
#
# file_install => {
#   '/tmp/dest_file' => {
#     source => 'rollout:/conf/source_file',
#     command => 'ls -l /tmp/dest_file',
#     mode => 0640,
#     uid => 1000,
#     gid => 1000,
#   },
#   '/tmp/dest_file' => {
#     text => 'This is some text\n',
#   },
# },
# symlink => {
#   '/tmp/dest' => '/tmp/source',
# },

validate_config {
  file_install => {
    type => "hash",
    key => {
      help => "Destination path",
      type => "path",
    },
    value => {
      type => "options",
      options => {
        command => { type => "string" },
        gid => { type => "int", range => [0, 65535] },
        group => { type => "string", regex => qr/^[a-z0-9_\-.]+$/ },
        mode => { type => "int", range => [0, 07777] },
        owner => { type => "string", regex => qr/^[a-z0-9_\-.]+$/ },
        source => { type => "string" },
        text => { type => "string" },
        uid => { type => "int", range => [0, 65535] },
      },
    },
  },
  symlink => {
    type => "hash",
    key => {
      help => "Symlink source (create this)",
      type => "path",
    },
    value => {
      help => "Symlink destination (symlink points to this)",
      type => "path",
    },
  },
  symlink_check => {
    type => "hash",
    deprecated => "symlink",
    key => {
      help => "Symlink source (create this)",
      type => "path",
    },
    value => {
      help => "Symlink destination (symlink points to this)",
      type => "path",
    },
  },
};

my %done_files;
my %files = flatten_hash(c("$hostname/file_install"));
while (my($dest, $f) = each(%files)) {
  next if $done_files{$dest}++;
  next if i_immutable_file($dest);
  next unless i_should($dest);
  if ($f->{source}) {
    file_install(-file => $dest, -src => $f->{source}, -cmd => $f->{command} || undef,
                 -mode => $f->{mode}, -uid => $f->{uid}, -gid => $f->{gid});
  } elsif ($f->{text}) {
    if ($f->{text} =~ /^\n?(\s+)/m) {
      my $strip = $1;
      v "Stripping \"$strip\" from the start of text for file: $dest";
      my @text = split(/\n/, $f->{text});
      $f->{text} = "";
      foreach (@text) { s/^$strip//; $f->{text} .= "$_\n"; };
    }
    text_install(-file => $dest, -text => $f->{text}, -cmd => $f->{command} || undef,
                 -mode => $f->{mode}, -uid => $f->{uid}, -gid => $f->{gid});
  }
}

my %symlinks = flatten_hash(c("$hostname/symlink_check"), c("$hostname/symlink"));
while (my($dest, $src) = each %symlinks)
{
  if (ref $src eq 'ARRAY')
  {
    my @list = uniq(@$src);
    validate("You have multiple symlink definitions for $dest, you must only have one.",
             @list == 1);
    $src = $list[0];
  }
  symlink_check($src, $dest);
}
